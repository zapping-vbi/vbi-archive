dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.23 2002-06-24 03:25:16 mschimek Exp $

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(rte, 0.5cvs)
AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE(m4)

dnl
dnl Default to -O2, except we are debugging -g
dnl
test -z "`echo $CFLAGS | grep -e -g`" && CFLAGS="-O2 $CFLAGS"

AC_PROG_CC
AM_PROG_AS

AM_PROG_LIBTOOL

AC_CHECK_FUNCS(memalign)
AC_CHECK_FUNCS(program_invocation_name)

dnl
dnl Test 'as' support for SSE2
dnl
use_sse2=no
AC_MSG_CHECKING([whether as supports SSE2])
AC_TRY_COMPILE(, __asm__ __volatile__ (" movdqu 0,%xmm0\n"),
  [AC_DEFINE(USE_SSE2, 1, [Define if 'as' supports SSE2])
    use_sse2=yes])
AC_MSG_RESULT($use_sse2)
AM_CONDITIONAL(USE_SSE2, test x$use_sse2 = xyes)

dnl ---------------------------------------------------------------------------
dnl Backends
dnl ---------------------------------------------------------------------------

BACKEND_MP1E=no
AC_ARG_WITH(mp1e,
  AC_HELP_STRING([--with-mp1e], [Add the mp1e backend]),
  BACKEND_MP1E="$withval", BACKEND_MP1E="yes")
if test "x$BACKEND_MP1E" = "xyes"; then
  AC_DEFINE(BACKEND_MP1E, 1, [Define if mp1e is built])

  dnl When builddir is different, no mp1e dir yet
  test -d mp1e || mkdir mp1e
  dnl Must not confuse with cli-mp1e config.h, hence not just -I
  ln -s ../config.h mp1e/config.h

  AC_CONFIG_FILES([
    mp1e/Makefile
    mp1e/video/Makefile
    mp1e/audio/Makefile
    mp1e/systems/Makefile
    mp1e/common/Makefile
  ])
fi

BACKEND_FFMPEG=no
AC_ARG_WITH(ffmpeg,
  AC_HELP_STRING([--with-ffmpeg], [Add the ffmpeg backend]),
  BACKEND_FFMPEG="$withval", BACKEND_FFMPEG="yes")
if test "x$BACKEND_FFMPEG" = "xyes"; then
  AC_DEFINE(BACKEND_FFMPEG, 1, [Define if ffmpeg is built])

      AC_DEFINE(ARCH_X86, 1, [ffmpeg config])
  dnl AC_DEFINE(ARCH_ARMV4L, 1, [ffmpeg config])
  dnl AC_DEFINE(ARCH_ALPHA, 1, [ffmpeg config])
      AC_DEFINE(HAVE_MMX, 1, [ffmpeg config])
  dnl AC_DEFINE(HAVE_GPROF, 1, [ffmpeg config])
      AC_DEFINE(CONFIG_ENCODERS, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_DECODERS, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_AC3, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_A52BIN, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_GRAB, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_MP3LAME, 1, [ffmpeg config])
  dnl AC_DEFINE(CONFIG_WIN32, 1, [ffmpeg config])
  dnl AC_DEFINE(SIMPLE_IDCT, 1, [ffmpeg config])
  dnl AC_DEFINE(WORDS_BIGENDIAN, 1, [ffmpeg config])
  dnl AC_DEFINE(HAVE_BYTESWAP_H, 1, [ffmpeg config])

  AC_CONFIG_FILES([
    ffmpeg/Makefile
    ffmpeg/libav/Makefile
    ffmpeg/libavcodec/Makefile
    ffmpeg/libavcodec/i386/Makefile
  ])
fi

BACKEND_DIVX4LINUX=no

AM_CONDITIONAL(BACKEND_FFMPEG, test "x$BACKEND_FFMPEG" = "xyes")
AM_CONDITIONAL(BACKEND_MP1E, test "x$BACKEND_MP1E" = "xyes")
AM_CONDITIONAL(BACKEND_DIVX4LINUX, test "x$BACKEND_DIVX4LINUX" = "xyes")

RTE_CFLAGS=-I`cd $srcdir; pwd`/src
AC_SUBST(RTE_CFLAGS)


for i in . mp1e; do
  test -d $i && test -e $i/site_def.h || cat <<EOF >$i/site_def.h
/* Site specific definitions */

#ifndef SITE_DEF_H
#define SITE_DEF_H
#endif /* SITE_DEF_H */
EOF
done

dnl
dnl Native language support.
dnl 
AM_GNU_GETTEXT([external], [need-ngettext])

dnl 
dnl Build docs from the sources automagically if gtkdoc is available.
dnl 
GTK_DOC_CHECK

AC_CONFIG_FILES([
  Makefile
  m4/Makefile
  po/Makefile.in
  docs/Makefile
  src/Makefile
  test/Makefile
  rte.spec
])

AC_OUTPUT

echo "
	Backend			Build
	-------			-----
	mp1e			$BACKEND_MP1E
	ffmpeg			$BACKEND_FFMPEG
	divx4linux		$BACKEND_DIVX4LINUX
"

dnl
dnl In cvs we have two build targets: rte with mp1e as backend and
dnl command line mp1e. The rte dist however will not build cli mp1e
dnl or vice versa.
dnl
test -e $srcdir/mp1e/configure.in && echo "
Note mp1e is now configured as rte backend. To build the mp1e command
line program, configure and make in mp1e/ instead.
"